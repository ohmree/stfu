# Maintainer: Me :)
pkgname=qt5-jsonserializer-minimal
pkgver=4.0.3
pkgrel=1
pkgdesc="A library to perform generic seralization and deserialization of QObjects"
arch=('i686' 'x86_64')
url="https://github.com/Skycoder42/QtJsonSerializer"
license=('BSD')
depends=('qt5-base')
provides=("qt5-jsonserializer=$pkgver")
conflicts=('qt5-jsonserializer')
makedepends=('git' 'qt5-tools')
_pkgfqn=$pkgname-$pkgver
source=("$_pkgfqn::git+https://github.com/Skycoder42/QtJsonSerializer.git#tag=$pkgver")
sha256sums=('SKIP')


prepare() {
  mkdir -p build
}

build() {
  cd build

  qmake CONFIG+=no_register_json_converters "../$_pkgfqn/"
  make
}

check() {
  cd build
  make run-tests
}

package() {
  cd build
  cd src/jsonserializer
  make INSTALL_ROOT="$pkgdir" install
  cd ../..

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -D -m644 "../$_pkgfqn/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
