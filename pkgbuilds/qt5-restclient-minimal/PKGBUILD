# Maintainer: Me :)
pkgname=qt5-restclient-minimal
pkgver=3.0.0
_pkgtag="${pkgver}-4"
pkgrel=4
pkgdesc="A library for generic JSON-based REST-APIs, with a mechanism to map JSON to Qt objects"
arch=('i686' 'x86_64')
url="https://github.com/Skycoder42/QtRestClient"
license=('BSD')
depends=('qt5-base' 'qt5-networkauth' 'qt5-jsonserializer-minimal>=4.0.0')
makedepends=('git' 'qt5-tools' 'qt5-declarative' 'qdep')
_pkgfqn=$pkgname-$pkgver
source=("$_pkgfqn::git+https://github.com/Skycoder42/QtRestClient.git#tag=$_pkgtag")
sha256sums=('SKIP')


prepare() {
  mkdir -p build
}

build() {
  cd build

  qmake "../$_pkgfqn/"
  make
}

check() {
  cd build
  make run-tests
}

package() {
  cd build
  cd src
  make INSTALL_ROOT="$pkgdir" install
  cd ../tools
  make INSTALL_ROOT="$pkgdir" install
  cd ..

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -D -m644 "../$_pkgfqn/qbs/Qt/restbuilder/module.qbs" "$pkgdir/usr/share/qbs/modules/Qt/restbuilder/module.qbs"

  install -D -m644 "../$_pkgfqn/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
